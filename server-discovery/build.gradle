buildscript {
	ext {
		springBootVersion = '1.5.8.RELEASE'
	}
	repositories {
		jcenter()
    	mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'com.bmuschko:gradle-docker-plugin:3.2.0'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

jar {
  	baseName = 'server-discovery'
  	version = ''
}

group = 'com.eureka.server'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	jcenter()
	mavenCentral()
}

ext {
	springCloudVersion = 'Dalston.SR4'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	compile('org.springframework.cloud:spring-cloud-starter-eureka-server')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  	destFile = project.file('build/libs/Dockerfile')
  	from 'openjdk:8u151-slim'
  	maintainer 'Alejandro GÃ³mez "alejandrocruz.x819@gmail.com"'
  	copyFile 'server-discovery.jar', '/opt/server-discovery.jar'
  	exposePort 8761
	entryPoint 'java', '-Djava.awt.headless=true', '-jar', '/opt/server-discovery.jar'
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  	dependsOn createDockerfile
  	inputDir = createDockerfile.destFile.parentFile
  	tag = 'alxgcruz/server-discovery:' + project.version.toLowerCase()
}

task pushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  	dependsOn buildImage
  	imageName = buildImage.tag
}

createDockerfile.dependsOn bootRepackage
